//xlang Source, Name:MemoryLookupWnd.xcsm 
//Date: Tue Jul 16:39:20 2019 

class MemoryLookupWnd : QXDockWidget{
	QXLineEdit edtaddress;
    QXPushButton btnLockup, dumpButton; 
	HexEditor memcontent;
    static MemoryLookupWnd memlookupWnd;
    
    static class caRegMixMath {

		static class Express{
			byte [] p;
			int start;
			int length;
			
            Express(String exp){
				p = exp.getBytes();
                start = 0;
                length = exp.length();
            }
            
			Express(byte [] _p,int _s,int _l){
				p = _p;
				start = _s;
				length = _l;
			}
			
			Express(Express p2) {
				p = p2.p;
				start = p2.start;
				length = p2.length;	
			}

			char getChar(){
				return p[start];
			}
			bool next(){
				if (start < length - 1){
					start++;
					return true;
				}
				return false;
			}
			long atoi(){
				int n = start;
                bool bhex = false;
                if (length > 1 && p[n] == '0' && p[n + 1] == 'x'){
					bhex = true;
                    n += 2;
                }
				while ((p[n] >= '0' && p[n] <= '9') || (p[n] >= 'a' && p[n] <= 'z' && bhex)){
					n++;
					if (n >= length){
						break;
					}
				}
				if (n > start){
					if (bhex){
						return new String(p, start, n - start).parseHex();
                    }
					return new String(p, start, n - start).parseLong();
				}
				return 0;
			}

			bool complete() {
				return (length -1) == start;
			}
		};
        
        static long calc(String text){
			return mixcalc(new Express(text), true, false);
        }
        
		static long mixcalc(Express p,bool bmain, bool b) {
			long result = 1;

			while ( p.getChar() != '(' && false == (p.getChar() >= '0' && p.getChar() <= '9') && false == (p.getChar() >= 'a' && p.getChar() <= 'z')) { result *= (p.getChar() == '-' ? -1 : 1); p.next(); }

			if (p.getChar() == '(') {
				p.next();
				result *= mixcalc(p, true, false);
				p.next();
			} else
				if (p.getChar() != 0) {
	
					result *= p.atoi();
					
					while (((p.getChar() >= '0' && p.getChar() <= '9') || (p.getChar() >= 'a' && p.getChar() <= 'z'))) { 
						if (p.next() == false){
							break;
						}
					}
				}

			if (p.complete() || b || (bmain == false && (p.getChar() == '+' || p.getChar() == '-'))) { return result; }

			do {
				char ch = p.getChar();
				switch (ch)
				{
					
					case '+':
						p.next();
						result += mixcalc(p,  false, false); 
					break;
						
					case '-':
						p.next();
						result -= mixcalc(p,  false, false); break;
						
					case '*':
						p.next();
						result *= mixcalc(p,  false, true); break;
						
					case '/':
						p.next();
						result /= mixcalc(p,  false, true); break;
					case ')':
					case '\0':
					default:
						return result; 
				}
				if (p.complete()){
					break;
				}
			} while (((p.getChar() == '*' || p.getChar() == '/') && (b == false)) || (bmain == true));
			return result;
		}
	};
    
    void dump(){
		String addrtext = edtaddress.getText().replace(" ", "");
		if (addrtext.length() > 0){
			long address = caRegMixMath.calc(addrtext.lower());
			XWorkspace.workspace.debuggee.lookupMemory(address, 512);
		}
    }
    
    void dumpFile(){
		String addrtext = edtaddress.getText().replace(" ", "");
		if (addrtext.length() > 0){
			long address = caRegMixMath.calc(addrtext.lower());
            
            InputDialog.requestInput(new InputDialog.onInputListener() {
                static String filePath = "";
                bool onInputOk(String text)override {
                    if (text.length() == 0 || text.isDigit() == false) {
                        QXMessageBox.Critical("错误", "输入了不正确的长度", QXMessageBox.Ok, QXMessageBox.Ok);
                        return false;
                    }
                    int length = text.parseInt();
                    
                    if (length < 0){
                        QXMessageBox.Critical("错误", "输入了不正确的长度", QXMessageBox.Ok, QXMessageBox.Ok);
                        return false;
                    }
                    
                    String file = QXFileDialog.getSaveFileName("保存文件", filePath,  "All Files(*.*)", MemoryLookupWnd.this);
                    if (file != nilptr && file.length() > 0) {
                        filePath = file;
                        dumpCore(file, address, length);
                    }
                    return true;
                }
                bool onInputCancel()override {
                    return true;
                }
                String getTitle()override {
                    return "转储内存";
                }
                String getTips()override {
                    return "输入转存长度:";
                }
                String getDefault()override {
                    return "";
                }
            });
			XWorkspace.workspace.debuggee.lookupMemory(address, 512);
		}
    }
    
    void dumpCore(String localFile, long address, long length){
        JsonObject json = new JsonObject();
        json.put("file", localFile);
        json.put("address", "" + address);
        json.put("length", "" + length);
		XWorkspace.workspace.debuggee.dumpMemory(address, 512, json.toString(false));
    }
    
	void onAttach(){
		memlookupWnd = this;
		edtaddress = (QXLineEdit)attachByName(new QXLineEdit(), "edtaddress");
        btnLockup = (QXPushButton)attachByName(new QXPushButton(), "btnLockup");
        dumpButton = (QXPushButton)attachByName(new QXPushButton(), "dumpButton");
        memcontent = (HexEditor)attachByName(new HexEditor(), "memcontent");
        
        btnLockup.setOnClickListener(new onClickListener(){
			void onClick(QXObject obj, bool checked)override{
				dump();
			}
        });
        
        dumpButton.setOnClickListener(new onClickListener(){
			void onClick(QXObject obj, bool checked)override{
				dumpFile();
			}
        });
        
        edtaddress.setOnKeyEventListener(new onKeyEventListener(){
			bool onKeyPress(QXObject obj, int key, bool repeat, int count, String text, int scanCode, int virtualKey, int modifier)override{
				if (key == Key_Enter || key == Key_Return){
					dump();
                    return false;
                }
                return true;
			}
        });
        memcontent.init();
    }
    
    static void SetData(JsonObject json){
		memlookupWnd.runOnUi(new Runnable(){
			void run()override{
				memlookupWnd.setData(json);
            }
        });
    }
    
    static void SetDumpData(JsonObject json){
		memlookupWnd.runOnUi(new Runnable(){
			void run()override{
				memlookupWnd.setDumpData(json);
            }
        });
    }
    
    static void reset(){
    	memlookupWnd.runOnUi(new Runnable(){
			void run()override{
				memlookupWnd.memcontent.setData(nilptr, 0, 0, 0);
            }
        });
    }
    
    void setData(JsonObject json){
		String dataCont = json.getString("content");
        String address =json.getString("address");
        int requestlen = json.getInt("request");
        int responselen = json.getInt("response");
        
        long offset = address.parseLong();
        byte[] data = Base64.decodeString(dataCont);
        memcontent.setData(data, offset, requestlen, responselen);
    }
    
    void setDumpData(JsonObject json){
		String dataCont = json.getString("content");
        String address = json.getString("address");
        String param = json.getString("param");
        int requestlen = json.getInt("request");
        int responselen = json.getInt("response");
        
        long offset = address.parseLong();
        
        
        byte[] data = Base64.decodeString(dataCont);
        FileOutputStream fos = nilptr;
        bool bContinue_req = false;
        bool dumpfailed = requestlen != responselen;
        String localFile = "";
        try{
            JsonObject localInfo = new JsonObject(param);
            localFile = localInfo.getString("file");
            long start = localInfo.getString("address").parseLong();
            long length = localInfo.getString("length").parseLong();
            long end_ofst = start + length;
            
            long req_endofst = offset + responselen;
            
            fos = new FileOutputStream(localFile, true); 
            
            if (end_ofst < req_endofst){
                fos.write(data, 0, end_ofst - offset);
            }else{
                fos.write(data);
            }
            
            if (false == dumpfailed){
                bContinue_req = (req_endofst < end_ofst);
            }
            
        }catch(Exception e){
            
        }finally{
            if (fos != nilptr){
                fos.close();
            }
        }
        
        if (bContinue_req){
            XWorkspace.workspace.debuggee.dumpMemory(offset + responselen, 512, param);
        }else{
            if (dumpfailed){
                XWndOutput.Output("转储内存文件:" + localFile + " 仅完成部分(错误:无法访问内存)\n");
            }else{
                XWndOutput.Output("转储内存文件:" + localFile + " 已完成!\n");
            }
        }
    }
    
    void reconfig(){
		memcontent.setStyle(Setting.isDarkStyle());
    }
};